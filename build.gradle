buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net/" }
        maven { url = "https://maven.gofancy.wtf/releases" }
    }

    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+"
        classpath("wtf.gofancy.fancygradle:wtf.gofancy.fancygradle.gradle.plugin:1.1.3-0")
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "wtf.gofancy.fancygradle"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net/"
    }

    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net"
    }

    maven {
        name = "JEI Maven"
        url = "https://dvs1.progwml6.com/files/maven"
    }

    maven {
        name = "OpenComputers Maven"
        url = "https://maven.cil.li/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"

    implementation fg.deobf("cofh:RedstoneFlux:${rf_mc_version}-${rf_version}:universal")
    implementation fg.deobf("cofh:CoFHCore:${mc_version}-${cofh_core_version}:universal")

    implementation fg.deobf("mezz.jei:jei_${jei_mc_version}:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei_${mc_version}:${jei_version}")

    implementation fg.deobf("li.cil.oc:OpenComputers:MC1.12.2-1.7.+")
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        "client" {
            workingDirectory file("./run")
            mods { "${project.name}" { source sourceSets.main } }
        }

        "server" {
            workingDirectory file("./run/server")
            mods { "${project.name}" { source sourceSets.main } }
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

//When Forge 1.12 loads mods from a directory that's been put on the classpath, it expects to find resources in the same directory.
//Default Gradle behavior puts resources in ./build/resources/main instead of ./build/classes/main/java. Let's change that.
sourceSets.all { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }